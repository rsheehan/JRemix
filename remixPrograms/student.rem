=
Student record
=

Students : {
	"Martin Developer" age 22 knows {
		"Java" : "Expert"
		"python" : "Advanced"
		"Remix" : "Expert"
	}
	"May Smith" age 25 knows {
		"C++" : "Advanced"
		"C" : "Expert"
		"C#" : "Competent"
	}
	"Ray Oflight" age 19 knows {
		"python" : "Novice"
	}
}

"Student data" ↲
"============" ↲
for each Student in Students do
	Student's Name ↲
	Student's Age ↲
	Student's Languages ↲
	↲

merge {"BASIC" : "Expert"} into (Students {2}'s Languages)

"Expertise" ↲
"=========" ↲
for each Student in Students do
	Student ↲

----------------

Name age Age knows Languages :
	create
		Name : Name
		Age : Age
		Languages : Languages

		getter
			Name, Age
			Languages

		MY expert languages :
			filter Languages by Language where
				Languages {Language} = "Expert"

		ME to string :
			Result : Name ⊕ " age " ⊕ Age ⊕ ", expert in "
			Expertise : MY expert languages
			if (no Expertise)
				Result ⊕ "nothing"
			… otherwise
				for each Language in Expertise keeping Count do
					when
						[Count = 1] do
							Result : Result ⊕ Language
						[Count < Expertise length] do
							Result : Result ⊕ ", " ⊕ Language
						Result ⊕ " and " ⊕ Language
