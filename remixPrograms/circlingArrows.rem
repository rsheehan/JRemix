using graphics lib

	===============
	Rotating Arrows
	===============

	the-Window : a graphics window
	the-Animation-Layer : the-Window next layer
	the-Arrows : 36 arrows
	place the-Arrows in the-Animation-Layer
	animate the-Arrows in the-Window

N arrows :
	Arrows : { }
	Angle : 0
	repeat N times
		the-Arrow : an arrow
		position the-Arrow at Angle
		add 10 to Angle
		append the-Arrow to Arrows
	return Arrows

an arrow:
	Arrow : a shape from {
		{ 0, 1 }
		{-2, 2 }
		{ 0,-2 }
		{ 2, 2 }
	}
	Arrow's Colour : a random colour
	return Arrow

position the-Arrow at Angle :
	X : CENTRE-X + 200 × cosine (Angle degrees as radians)
	Y : CENTRE-Y - 200 × sine (Angle degrees as radians)
	the-Arrow's Position : { X, Y }
	the-Arrow's Heading : -(Angle degrees as radians)

animate the-Arrows in the-Window :
	animate the-Window 180 times at 30 ticks per second
		for each Arrow in the-Arrows
			Angle : -(Arrow's Heading) radians as degrees
			add 2 to Angle
			position Arrow at Angle
