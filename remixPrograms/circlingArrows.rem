using graphics lib

	===============
	Rotating arrows
	===============

	'the window' : the graphics panel
	'the animation layer' : 'the window' next layer
	'arrows' : 36 arrows
	place 'arrows' in 'the animation layer'
	animate 'arrows' in 'the window'

'n' arrows :
	'arrows' : { }
	'angle' : 0
	repeat 'n' times
		'the arrow' : an arrow
		position 'the arrow' at 'angle'
		add 10 to 'angle'
		append 'the arrow' to 'arrows'

an arrow:
	'arrow' : a shape from {
		{ 0, 1 }
		{-2, 2 }
		{ 0,-2 }
		{ 2, 2 }
	}
	'arrow'’s 'colour' : a random colour
	'arrow'

position 'the arrow' at 'angle' :
	'x' : CENTRE-X + 200 × cosine ('angle' degrees as radians)
	'y' : CENTRE-Y - 200 × sine ('angle' degrees as radians)
	'the arrow'’s 'position' : { 'x', 'y' }
	'the arrow'’s 'heading' : -'angle' degrees as radians

animate 'arrows' in 'the window' :
	animate 'the window' 180 times at 30 ticks per second
		for each 'arrow' in 'arrows'
			'angle' : -('arrow'’s 'heading') radians as degrees
			add 2 to 'angle'
			position 'arrow' at 'angle'
