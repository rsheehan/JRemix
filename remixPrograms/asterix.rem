"Asterisk Diamonds" ↲
"=================" ↲
↲

for each Size from 1 to 41 in steps of 2 do
	Size, "." ↲
	print a diamond of Size
	↲

-==============================================================================-

=-
Show a diamond of "Size".
=-
print a diamond of Size :
	show top half of Size diamond and fill in Lines
	show middle line of Size diamond
	show bottom half of Size diamond with Lines

=-
Show the top half of the diamond of "Size".
Fill in the "Lines" list at the same time.
=-
show top half of Size diamond and fill in #Lines :
	#Lines : {}
	if (Size ≥ 3)
		for each Row from 1 to (Size ÷ 2) do
			num-Spaces : Size - Row × 2
			num-Stars : Row × 2 - 1
			Spaces : " " num-Spaces times
			Stars : " *" num-Stars times
			#Lines {Row} : Spaces ⊕ Stars
			#Lines {Row} ↲

=-
Show the middle line of the diamond of "Size".
=-
show middle line of Size diamond :
	"* " Size times ↲

=-
Show the bottom half of the diamond of "Size".
Uses the "Lines" list.
=-
show bottom half of Size diamond with Lines :
	if (Size ≥ 3)
		for each Row from (Size ÷ 2) to 1 do
			Lines {Row} ↲
