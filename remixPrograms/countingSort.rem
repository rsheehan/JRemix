- The Counting Sort -

MIN : 0
MAX : 100
'list' : a random list of 1000 numbers from MIN to MAX

'list' ↲

sort 'list' with values from MIN to MAX

↲ . 'list' ↲

---------------------------------------------

a random list of 'size' numbers from 'start' to 'finish' :
	apply
		random from 'start' to 'finish'
	… 'size' times

sort #list with values from 'start' to 'finish' :
	'range' : 'finish' - 'start' + 1
	'collect counts' : apply [0] 'range' times

	for each 'value' in #list
		'place' : 'value' - 'start' + 1
		increment ('collect counts' {'place'})

	#list : { }
	for each 'item' in 'collect counts' keeping 'index'
		repeat
			append ('start' + 'index' - 1) to #list
		… 'item' times
