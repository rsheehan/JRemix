using graphics, turtle

	the mountains :
		apply
			a DARK-GREY mountain with BLACK edges of height 300 based at 800
		… 15 times
		
	add 'the mountains' on the 'layer' :
		for each 'mountain' in 'the mountains'
			place 'mountain' in 'layer'
			
	a 'colour' mountain with 'outline colour' edges of height 'max' based at 'level' :
		'centre' : random STD-WIDTH
		'height' : random 'max' + 20
		'width'  : random 150 + 300
		'mountain' : a 'colour' shape from {
			{ 0, -'height' }
			{ 'width' ÷ 2, 0 }
			{ -'width' ÷ 2, 0 }
		} scaled by 1 at { 'centre', 'level' }
		'mountain' outline colour 'outline colour'
		'mountain'
	
	star explosion in 'space' :
		'40 turtles' : a group of 40 turtles
		'point' : { random STD-WIDTH - CENTRE-X, CENTRE-Y - random 500 }
		'angle' : 0
		for each 'turtle' in '40 turtles'
			'turtle' heading  (add 9 to 'angle')
			'turtle' colour   (random colour)
			'turtle' position 'point'
			'turtle' transient true
		place '40 turtles' group in 'space'
		'40 turtles'
		
	'explosion' effect : 
		'explosion' speed (4 + random 6)
		move 'explosion' (random from 50 to 200) steps

---------------------------------------------------
	'the window' : the graphics panel
	'turtle space' : a turtle space in 'the window'
---------------------------------------------------
'back layer'  : 'the window' base layer
'the mountains' : the mountains
add 'the mountains' on the 'back layer'
repeat 40 times
	'explosion' : star explosion in 'turtle space'
	'explosion' effect
	clear 'the window' leaving the base layer
