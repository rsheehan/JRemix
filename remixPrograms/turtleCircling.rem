with turtle graphics

	===============
	Rotating Arrows
	===============

	the-Arrows : a group of 36 turtles
	place the-Arrows group in (turtle space in (the graphics panel))
	Angle : 0
	for each Arrow in the-Arrows
		Arrow pen up
		Arrow's Colour : a random colour
		Arrow's Heading : Angle
		add 10 to Angle

	the-Arrows' Speed : 200
	RADIUS : 200
	position the-Arrows

	the-Arrows' Speed : 5
	STEP : 2 × RADIUS × π ÷ 180
	repeat 180 times
		animate the-Arrows

an arrow:
	a shape from {
		{ 0, 1 }
		{-2, 2 }
		{ 0,-2 }
		{ 2, 2 }
	}

position the-Arrows :
	move the-Arrows RADIUS steps
	turn the-Arrows -90 degrees

animate the-Arrows :
	move the-Arrows STEP steps
	turn the-Arrows -2 degrees
