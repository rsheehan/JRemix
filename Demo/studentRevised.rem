=
Student record
=

students : {
	a programmer "Martin Developer" age 22 knows {
		Java   : "Expert"
		python : "Advanced"
		Remix  : "Expert"
	}
	a programmer "May Smith" age 25 knows {
		C++ : "Expert"
		C   : "Expert"
		C#  : "Competent"
	}
	a programmer "Ray Oflight" age 19 knows {
		python : "Novice"
		Java   : "Expert"
	}
}

"Student data" ↲
"============" ↲
for each (student) in (students)
	student's name ↲
	student's age ↲
	student's languages ↲
	↲

merge {BASIC : "Expert"} into (students{2}'s languages)

"Expertise" ↲
"=========" ↲
for each (student) in (students)
	student ↲
↲
the "Java" experts amongst the (students)
the "C++" experts amongst the (students)
the "Remix" experts amongst the (students)

----------------

the (language) experts amongst the (group) :
	experts : filter (group) by (programmer) where
		(programmer) is an expert in (language)
	… keeping
		programmer's name

	"The ", language, " expert", is or are ((experts) length) ," : " ~
	experts ↲

is or are (number) :
	when
		[number = 1] do [" is"]
		"s are"

a programmer (name) age (years) knows (languages) :
	create
		name : name
		age : years
		languages : languages

		getter
			name, age
			languages

		(my) expert languages :
			filter (languages) by (language) where
				languages{language} = "Expert"

		(me) is an expert in (language) :
			languages{language} = "Expert"

		(me) to string :
			result : name ⊕ " age " ⊕ age ⊕ ", expert in "
			expertise : (my) expert languages
			if (no (expertise))
				result ⊕ "nothing"
			… otherwise
				for each (language) in (expertise) keeping (count)
					when
						[count = 1] do
							result : result ⊕ language
						[count < (expertise) length] do
							result : result ⊕ ", " ⊕ language
						result ⊕ " and " ⊕ language
