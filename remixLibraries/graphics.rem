===============================
Graphics Operations and Objects
===============================
library "edu.fizz.remix.libraries.Graphics"

	=====================
	Dimensions and points
	=====================
	STD-WIDTH  : 1000
	STD-HEIGHT : 800
	CENTRE-X   : STD-WIDTH ÷ 2
	CENTRE-Y   : STD-HEIGHT ÷ 2
	THE-CENTRE-POINT : { CENTRE-X, CENTRE-Y }

	=-
	A random point in the standard graphics window.
	=-
	random point :
		{ random STD-WIDTH, random STD-HEIGHT }

	=======
	Colours
	=======
	WHITE     : { 255, 255, 255 }
	BLACK     : { 0, 0, 0 }
	RED       : { 255, 0, 0 }
	GREEN     : { 0, 255, 0 }
	BLUE      : { 0, 0, 255 }
	YELLOW    : { 255, 255, 0 }
	DARK-BLUE : { 0, 0, 50 }
	SKY-BLUE  : { 180, 230, 255 }
	GREY      : { 128, 128, 128 }
	DARK-GREY : { 64, 64, 64 }

	=-
	A random colour.
	=-
	random colour :
		{ random 255, random 255, random 255 }

-==================================================================================================-

	================
	Graphics objects
	================

	======
	Shapes
	======

	=-
	Create a triangle.
	=-
	a triangle :
		a 3 sided polygon

	=-
	Create a square.
	=-
	a square :
		a 4 sided polygon

	=-
	Create an 'n' sided polygon.
	=-
	a/an 'n' sided polygon :
		'points' : { }
		for each 'side' from 0 to ('n' - 1)
			append {
				cosine (2π × 'side' ÷ 'n')
				sine (2π × 'side' ÷ 'n')
			} to 'points'
		shape from 'points' scaled by 20

	=-
	Create a box centred at 'position'
	with 'colour' and 'width' x 'height'.
	=-
	'colour' box of 'width' x 'height' at 'position' :
		'left'   : -'width' ÷ 2
		'right'  : 'width' ÷ 2
		'top'    : -'height' ÷ 2
		'bottom' : 'height' ÷ 2
		'colour' shape from {
			{ 'left', 'top' }
			{ 'right', 'top' }
			{ 'right', 'bottom' }
			{ 'left', 'bottom' }
		} scaled by 1 at 'position'

	=-
	Create a shape from the list of 'points'.
	The shape is scaled by 5.
	=-
	shape from 'points' :
		shape from 'points' scaled by 5

	=-
	Create a shape from the list of 'points' scaled by 'size'.
	=-
	shape from 'points' scaled by 'size' :
		RED shape from 'points' scaled by 'size' at THE-CENTRE-POINT

	=-
	Create a shape of 'colour' from the list of 'points' scaled by 'size'
	at 'position'.
	'shapes' can be filled or just outlines.
	=-
	'colour' shape from 'points' scaled by 'size' at 'position':

		extend shape at 'position'
			'type'           : "shape"
			'colour'         : 'colour'
			'outline colour' : null ; no outline
			'polygon'        : 'points'
			'size'           : 'size'
			'heading'        : 0.0 ; radians

			getters/setters
				'colour', 'outline colour',
				'size', 'heading'

			getter
				'type'

			=-
			Change shape 'OBJECT' heading by 'δd' degrees.
			=-
			change MY heading by 'δd' degrees :
				add ('δd' degrees as radians) to 'heading'

	=-
	Create an object with a position.
	=-
	shape at 'position' :
		create
			'position' : 'position'

			getter/setter
				'position'

			MY 'x-pos' : ; a fake getter
				'position'{1}

			MY 'y-pos' : ; a fake getter
				'position'{2}

			=-
			Change shape 'OBJECT' x-position by 'δx'.
			=-
			change MY x-pos by 'δx' :
				add 'δx' to ('position'{1})

			=-
			Change shape 'OBJECT' y-position by 'δy'.
			=-
			change MY y-pos by 'δy' :
				add 'δy' to ('position'{2})

	=====
	Lines
	=====

	=-
	Create a 'colour' line from 'start' to 'finish'.
	The width is 3 pixels by default.
	=-
	'colour' line from 'start' to 'finish' :
		create
			'type'   : "line"
			'start'  : 'start'
			'finish' : 'finish'
			'width'  : 3
			'colour' : 'colour'

			getters/setters
				'start', 'finish', 'width', 'colour'

			change MY x-pos by 'δx' :
				add 'δx' to ('start'{1})
				add 'δx' to ('finish'{1})

			change MY y-pos by 'δy' :
				add 'δy' to ('start'{2})
				add 'δy' to ('finish'{2})

	=-
	Create a green line from 'start' to 'finish'.
	=-
	a line from 'start' to 'finish' :
		a GREEN line from 'start' to 'finish'

	=-
	Create a random line.
	=-
	random line :
		(random colour) line from (random point) to (random point)

	=======
	Circles
	=======

	=-
	Create a 'colour' circle of radius 'radius' at 'position'.
	=-
	'colour' circle of radius 'radius' at 'position' :
		extend shape at 'position'
			'type'     : "circle"
			'colour'   : 'colour'
			'outline colour' : null ; no outline
			'radius'   : 'radius'

			getters/setters
				'colour', 'outline colour',
				'radius'

	=-
	Create a random circle.
	=-
	random circle :
		'size' : random (STD-HEIGHT ÷ 8) + 10
		(random colour) circle of radius 'size' at (random point)

-==================================================================================================-

	===============
	Graphics Panels
	===============

	=-
	Open the graphic panel with multiple layers.
	Return the graphics panel.
	=-
	the graphics panel :
		(open graphics panel) with layers

	===========================================================
	The Remix 'panel' for drawing in.
	It has a base-layer which is an image for permanent shapes,
	and multiple layers for animation on top of that.

	It can be the built-in panel from the Remix IDE or a panel
	in a graphics window created outside this.
	===========================================================
	=-
	Return a panel with multiple layers based on 'JGraphics panel'.
	=-
	'JGraphics panel' with layers :
		'panel' : create
			'JGraphics' : 'JGraphics panel'
			'layers' : { a base-layer for 'JGraphics panel' }
			'colour' : DARK-BLUE ; set by "MY 'colour' 'the colour'"

			getters
				'layers', 'background colour'

			MY 'colour' 'the colour' : ; this is a setter
				'colour' : 'the colour'
				make the 'JGraphics' background 'colour' ; Java

			=-
			Add and return a new shape layer for the panel 'OBJECT'.
			=-
			MY next layer :
				'new layer' : a shape layer
				append 'new layer' to 'layers'
				'new layer'

			=-
			Get the permanent base-layer for the panel 'OBJECT'.
			=-
			MY 'base-layer' : ; a fake getter
				'layers' {1}

			=-
			Place the 'shape' in the base-layer of the panel 'OBJECT'.
			=-
			place 'shape' in ME :
				place 'shape' in ('layers' {1})

			=-
			Update the panel 'OBJECT'.
			This redraws all layers.
			=-
			update ME :
				clear transient 'JGraphics' layers ; Java
				for each 'layer' in 'layers'
					prepare 'layer' shapes for drawing on 'JGraphics'
				refresh 'JGraphics' ; Java

			==================
			Clears all layers.
			==================
			=-
			Clear all layers of the panel 'OBJECT' including the base-layer.
			=-
			clear ME :
				clear 'JGraphics' base-layer
				clear ME leaving the base-layer

			=-
			Clear all layers of the panel 'OBJECT' keeping the base-layer.
			=-
			clear ME leaving the base-layer :
				make the 'JGraphics' background 'colour'
				'layers' : { 'layers' {1} }
				update ME

		clear 'panel'
		'panel'

	================
	Graphics Windows
	================
	=-
	Open a graphics window with multiple layers.
	Return the graphics panel.
	=-
	a graphics window :
		'JWindow' : a "Drawing" window of STD-WIDTH by STD-HEIGHT
		show 'JWindow' ; Java
		'JGraphics panel' : the 'JWindow' graphics panel ; Java
		'JGraphics panel' with layers

	=-
	Create a shape layer.
	'shapes' get added to layers.
	This does not include the base-layer which is special.
	Animations should be placed in ordinary shape layers.
	=-
	a shape layer :
		create
			'shapes' : { }

			getter
				'shapes'

			=-
			Place the 'shape' in the shape layer 'OBJECT'.
			=-
			place 'shape' in ME :
				if ('shape' is a "list")
					for each 'part' in 'shape'
						append 'part' to 'shapes'
				… otherwise
					append 'shape' to 'shapes'

			=-
			Clear all shapes from the shape layer 'OBJECT'.
			=-
			clear MY shapes :
				'shapes' : { }

			=-
			Prepare and show the shapes of the shape layer 'OBJECT' on the 'JGraphics' panel.
			Does nothing to a base-layer.
			=-
			prepare MY shapes for drawing on 'JGraphics' :
				for each 'shape' in 'shapes'
					add the 'shape' to the 'JGraphics' ; Java

	=-
	Create a base-layer for the 'JGraphics panel'.
	'shapes' which get added to a base-layer are "permanent"
	until the layer gets cleared.
	Animations should not be placed in the base-layer.
	=-
	a base-layer for 'JGraphics panel' :
		create
			'JGraphics panel' : 'JGraphics panel' ; base layer can change so need this

			MY 'shapes' : ; a getter for shapes which are not stored
				{ }

			place 'shape' in ME :
				if ('shape' is a "list")
					for each 'part' in 'shape'
						add the 'part' to the ('JGraphics panel' base-layer) image ; Java
				… otherwise
					add the 'shape' to the ('JGraphics panel' base-layer) image ; Java

			=-
			Nothing happens in the base-layer.
			=-
			clear MY shapes :
				"Clearing shapes from the base-layer - does nothing."

			=-
			Nothing happens in the base-layer.
			=-
			prepare MY shapes for drawing on 'JGraphics' :
				"Preparing shapes in the base-layer - does nothing."

-==================================================================================================-

	=========
	Animation
	=========

	=-
	Create an animation object which executes 'changes' every 'n' ticks.
	=-
	every 'n' tick/ticks ['changes'] :
		create
			'ticks' : 'n'
			'counter' : 'n'
			'changes' : 'changes'

			animate ME :
				decrement 'counter'
				if ('counter' = 0)
					'counter' : 'ticks'
					do 'changes'

	=======================================================================
	The Java animation function is
	animate at 'n' ticks per second [Animation-block] until [Condition-block]
	=======================================================================

	=-
	Animate 'block' in 'the window' at 'n' ticks per second.
	=-
	animate 'the window' 'number' times at 'n' ticks per second ['block'] :
		animate at 'n' ticks per second
			do 'block'
			update 'the window'
			decrement 'number'
		… until ['number' < 1]

-==================================================================================================-
